! MappedGeometry_3D_Cuda_Class.f90
! 
! Copyright 2015 Joseph Schoonover <schoonover.numerics@gmail.com>, The Florida State University
! Copyright 2016 Joseph Schoonover <jschoonover@lanl.gov>, Los Alamos National Laboratory
!
! The SELF and accompanying documentation were produced in part under the 
! support of Florida State University and the National Science Foundation 
! through Grant OCE-1049131 during 2015 and in part  the support of the 
! Center for Nonlinear Studies and the Department of Energy through the 
! LANL/LDRD program in 2016.
!
! MappedGeometry_3D_Cuda_Class.f90 is part of the Spectral Element Libraries in Fortran (SELF).
! 
! Licensed under the Apache License, Version 2.0 (the "License"); 
! You may obtain a copy of the License at 
!
! http://www.apache.org/licenses/LICENSE-2.0 
!
! Unless required by applicable law or agreed to in writing, software 
! distributed under the License is distributed on an "AS IS" BASIS, 
! WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. 
! See the License for the specific language governing permissions and  
! limitations under the License.
!
! //////////////////////////////////////////////////////////////////////////////////////////////// !

MODULE MappedGeometry_3D_Cuda_Class

! src/common/
USE ModelPrecision
! src/geom/
USE MappedGeometry_3D_Class

IMPLICIT NONE


   TYPE MappedGeometry_3D_Cuda

      
      REAL(prec), ALLOCATABLE, DEVICE :: nHat_dev(:,:,:,:,:) 
      REAL(prec), ALLOCATABLE, DEVICE :: xBound_dev(:,:,:,:,:) 
      REAL(prec), ALLOCATABLE, DEVICE :: x_dev(:,:,:,:)
      REAL(prec), ALLOCATABLE, DEVICE :: J_dev(:,:,:,:)    
      REAL(prec), ALLOCATABLE, DEVICE :: Ja_dev(:,:,:,:,:,:)

      CONTAINS

      PROCEDURE :: Build      => Build_MappedGeometry_3D_Cuda
      PROCEDURE :: Trash      => Trash_MappedGeometry_3D_Cuda
      
      PROCEDURE :: UpdateDevice => UpdateDevice_MappedGeometry_3D_Cuda

   END TYPE MappedGeometry_3D_Cuda


 CONTAINS
!
!
!==================================================================================================!
!------------------------------- Manual Constructors/Destructors ----------------------------------!
!==================================================================================================!
!
!
 ATTRIBUTES(Host) SUBROUTINE Build_MappedGeometry_3D_Cuda( myGeom, N, nElems )

  IMPLICIT NONE
  CLASS(MappedGeometry_3D_Cuda), INTENT(out) :: myGeom
  INTEGER, INTENT(in)                        :: N, nElems

      ALLOCATE( myGeom % Ja_dev(0:N,0:N,0:N,1:3,1:3,1:nElems), &
                myGeom % J_dev(0:N,0:N,0:N,1:nElems), &
                myGeom % x_dev(0:N,0:N,0:N,1:nElems), &
                myGeom % xBound_dev(1:3,0:N,0:N,1:6,1:nElems), & 
                myGeom % nHat_dev(1:3,0:N,0:N,1:6,1:nElems) )
      
      myGeom % Ja_dev      = 0.0_prec
      myGeom % J_dev       = 0.0_prec
      myGeom % x_dev       = 0.0_prec
      myGeom % xBound_dev  = 0.0_prec
      myGeom % nHat_dev    = 0.0_prec

 END SUBROUTINE Build_MappedGeometry_3D_Cuda
!
 ATTRIBUTES(Host) SUBROUTINE Trash_MappedGeometry_3D_Cuda( myGeom )

   IMPLICIT NONE
   CLASS(MappedGeometry_3D_Cuda), INTENT(inout)  :: myGeom

      
      DEALLOCATE( myGeom % Ja_dev, myGeom % J_dev, myGeom % x_dev, &
                  myGeom % xBound_dev, myGeom % nHat_dev)
 
 END SUBROUTINE Trash_MappedGeometry_3D_Cuda
!
!
!==================================================================================================!
!--------------------------------- Type Specific Routines -----------------------------------------!
!==================================================================================================!
!
!
 ATTRIBUTES(Host) SUBROUTINE UpdateDevice_MappedGeometry_3D_Cuda( myGeom, cpuGeomArray, N, nElems )
 
   IMPLICIT NONE
   CLASS( MappedGeometry_3D_Cuda ), INTENT(inout) :: myGeom
   INTEGER, INTENT(in)                            :: N, nElems
   TYPE( MappedGeometry_3D ), INTENT(in)          :: cpuGeomArray(1:nElems)
   ! Local
   INTEGER :: i, j, iEl
   REAL(prec) :: Jac(0:N,0:N,0:N,1:nElems)
   REAL(prec) :: nHat(1:3,0:N,0:N,1:6,1:nElems)
   REAL(prec) :: xBound(1:3,0:N,0:N,1:6,1:nElems)
   REAL(prec) :: Ja(0:N,0:N,0:N,1:3,1:3,1:nElems)

      DO iEl = 1, nElems
	     Jac(:,:,:,iEl)      = cpuGeomArray(iEl) % J
	     nHat(:,:,:,:,iEl)   = cpuGeomArray(iEl) % nHat
	     xBound(1,:,:,:,iEl) = cpuGeomArray(iEl) % xbound
	     xBound(2,:,:,:,iEl) = cpuGeomArray(iEl) % ybound
	     xBound(3,:,:,:,iEl) = cpuGeomArray(iEl) % zbound
        Ja(:,:,:,:,:,iEl)   = cpuGeomArray(iEl) % Ja
	   ENDDO
      
      myGeom % J_dev      = Jac
      myGeom % nHat_dev   = nHat
      myGeom % xBound_dev = xBound
      myGeom % Ja_dev     = Ja
      
   END SUBROUTINE UpdateDevice_MappedGeometry_3D_Cuda
!
END MODULE MappedGeometry_3D_Cuda_Class
